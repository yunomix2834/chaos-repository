plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.protobuf' version '0.9.5'
}

group = 'com.chaos'
version = '0.0.1-SNAPSHOT'
description = 'Chaos Task Manager'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2025.1.0")
	set('springGrpcVersion', "1.0.2")
	set('springShellVersion', "4.0.1")

	set('grpcVersion', "1.64.0")
	set('protobufVersion', "3.25.3")
	set('natsVersion', "2.20.5")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
//	implementation 'org.springframework.boot:spring-boot-starter-elasticsearch'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'

	// gRPC client
	implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"
	implementation 'io.grpc:grpc-services'

	// Protobuf runtime
	implementation "com.google.protobuf:protobuf-java:${protobufVersion}"

	// NATS JetStream
	implementation "io.nats:jnats:${natsVersion}"

	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	implementation 'org.springframework.grpc:spring-grpc-spring-boot-starter'
	implementation 'org.springframework.shell:spring-shell-starter'

	// POI
	implementation 'org.apache.poi:poi:5.5.1'
	implementation 'org.apache.poi:poi-ooxml:5.5.1'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-actuator-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-elasticsearch-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webflux-test'
	testImplementation 'org.springframework.grpc:spring-grpc-test'
	testImplementation 'org.springframework.shell:spring-shell-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.shell:spring-shell-dependencies:${springShellVersion}"
		mavenBom "org.springframework.grpc:spring-grpc-dependencies:${springGrpcVersion}"
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protobufVersion}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generatedFilesBaseDir = "$projectDir/build/generated"
	generateProtoTasks {
		all()*.plugins {
			grpc { option '@generated=omit' }
		}
	}
}


sourceSets {
	main {
		proto {
			srcDir "${projectDir}/../proto"
		}
		java {
			srcDirs "$projectDir/src/main/java",
					"$projectDir/build/generated/source/proto/main/java",
					"$projectDir/build/generated/source/proto/main/grpc"
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
